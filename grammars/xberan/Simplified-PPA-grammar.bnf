#Simplified PPA grammar
#- no debris
#- task: grab food and move it to base (?)
#- postconditions: 
# Control flow

<root> ::= <sequence> | <selector>

<sequence> ::= [Sequence]<ppa>[/Sequence] | [Sequence]<root><root>[/Sequence] | [Sequence]<sequence><root>[/Sequence]

<selector> ::= [Selector]<ppa>[/Selector] | [Selector]<root><root>[/Selector] | [Selector]<selector><root>[/Selector]

<ppa> ::= [Selector]<postconditions><ppasequence>[/Selector]


# PPA control flow

<postconditions> ::= <SuccessNode> | <ppa> |[Sequence]<postcondition>[/Sequence] # None | another PPA | some

<postcondition> ::= <postcondition>[PostCnd]<postconditiont>[/PostCnd] | [PostCnd]<postconditiont>[/PostCnd]

<postconditiont> ::= NeighbourObjects_<objects> | IsCarrying_<dobjects>

<ppasequence> ::= [Sequence] <preconditions> [Act]<action>[/Act] [/Sequence]

<preconditions> ::= [Sequence]<precondition>[/Sequence]

<precondition> ::= <precondition>[PreCnd]<preconditiont>[/PreCnd] | [PreCnd]<preconditiont>[/PreCnd]

<preconditiont> ::= CanCarry_<dobjects> | CanDrop_<dobjects> | NeighbourObjects_<objects> | NeighbourObjects_<objects>_invert | IsCarrying_<dobjects> | IsCarrying_<dobjects>_invert

<action> ::= PPAMoveTowards_<objects> | PPARandomWalk | PPAPickUp_<objects> | PPADrop_<objects> | PPAMoveAway_<objects>

<objects> ::= <sobjects> | <dobjects>

<sobjects> ::= Hub

<dobjects> ::= Food

<SuccessNode> ::= [PostCnd]DummyNode[/PostCnd]
